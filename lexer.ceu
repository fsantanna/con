data :Tk = [pos :Pos, str] {
    :Sym = []
    :Tag = []
    :Num = []
}

export [iter-lexer] {
    func iter-skip (^buf :Buffer) {
        iter <-- coro () {
            loop {
                val c = ^^buf->read-c()
            } while c {
                ifs c {
                    in? [' ','\t','\n'] {}
                    == ';' {
                        val c' = ^^buf->read-c()
                        if c' /= ';' {
                            ^^buf->unread-c()
                        } else {
                            ^^buf->read-until-c('\n')
                        }
                    }
                    else => yield(c)
                }
            }
        }
    }

    do {
        println(:ITER-SKIP)
        val buf = buf-init(iter("1 2 ;; 9\n3 ;;"))
        val skip :Iterator = iter-skip(buf)
        skip->next() --> \{
            assert(it == '1')
        }
        skip->next() --> \{
            assert(it == '2')
        }
        skip->next() --> \{
            assert(it == '3')
        }
    }

    func iter-lexer (^inp :Iterator) {
        iter <-- coro () {
            val buf  :Buffer   = buf-init(^^inp)
            val skip :Iterator = iter-skip(buf)
            loop {
                val c = skip->next()
                val pos = copy(buf.cur.pos)
            } while c {
                ifs c {
                    in? ['@','#','(',')','[',']',',','-'] {
                        yield(:Tk.Sym [move(pos), to-string(c)])
                    }
                    == ':' {                ;; TAG
                        buf->read-while-f(letter-or-digit?) --> \{
                            yield(:Tk.Tag [move(pos), to-string(c) ++ it])
                        }
                    }
                    {{digit?}} {            ;; NUM
                        buf->unread-c()
                        buf->read-while-f \{
                            (it == '.') or letter-or-digit?(it)
                        } --> \{
                            yield(:Tk.Num [move(pos),move(it)])
                        }
                    }
                    else => throw("invalid token")
                }
            }
        }
    }

    do {
        println(:ITER-LEXER)
        val itr = iter-file("x.ceu")
        val lexer :Iterator = iter-lexer(itr)
        lexer->next() --> \{
            assert(it === :Tk.Num [[1,1],"1"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[1,3],"2"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[2,1],"3"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Tag [[3,5],":x"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,1],"@"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,2],"["])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,3],"("])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[4,4],"10"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,6],","])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[4,7],"20"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,9],")"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,10],"]"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,12],"#"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,13],"["])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,14],"-"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[4,15],"1"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[4,16],","])
        }
        lexer->next() --> \{
            assert(it === :Tk.Num [[4,17],":x"])
        }
        lexer->next() --> \{
            assert(it === :Tk.Sym [[4,19],"]"])
        }
    }
}
