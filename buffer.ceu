data :Pos = [lin,col]
data :Chr = [pos :Pos, c]
data :Buf = [itr :Iterator, prv :Chr, cur :Chr]

export [
    buf-init,
    buf-read-c, buf-unread-c,
    buf-read-until-f, buf-read-until-c,
    buf-read-while-f, buf-read-while-c,
] {
    func buf-init (inp :Iterator) {
        [inp, nil, [[1,0],nil]]
    }

    func buf-read-c (buf :Buf) {
        if buf.prv {
            set buf.cur = buf.prv
            set buf.prv = nil
            buf.cur.c
        } else {
            val c = buf.itr.f(buf.itr)
            set buf.cur.c = c
            set buf.cur.pos = if (c == '\n')
                -> [buf.cur.pos.lin+1 , 0]
                -> [buf.cur.pos.lin   , buf.cur.pos.col+1]
            c
        }
    }

    func buf-unread-c (buf :Buf) {
        assert(buf.prv == nil)
        set buf.prv = buf.cur
        set buf.cur = nil
        nil
    }

    do {
        println(:F-READ-UNREAD)
        val buf :Buf = buf-init(iter("12"))
        buf-read-c(buf) thus {
            assert(buf === [buf.itr, nil, [[1,1],'1']])
        }
        buf-unread-c(buf) thus {
            assert(buf === [buf.itr, [[1,1],'1'],nil])
        }
        buf-read-c(buf) thus {
            assert(buf === [buf.itr, nil,[[1,1],'1']])
        }
        buf-read-c(buf) thus {
            assert(buf === [buf.itr, nil,[[1,2],'2']])
        }
        buf-unread-c(buf) thus {
            assert(buf === [buf.itr, [[1,2],'2'],nil])
        }
        buf-read-c(buf) thus {
            assert(buf === [buf.itr, nil,[[1,2],'2']])
        }
    }

    func buf-read-until-f (buf, f-pred) {
        val str = #[]
        loop {
            val c = buf-read-c(buf)
        } until f-pred(c) {
            set str[+] = c
        }
        move(str)
    }

    func buf-read-until-c (buf, c) {
        buf-read-until-f(buf, \{ it == c })
    }

    func buf-read-while-f (buf, f-pred) {
        defer {
            buf-unread-c(buf)
        }
        buf-read-until-f(buf, \{ not f-pred(it) })
    }

    func buf-read-while-c (buf, c) {
        buf-read-while-f(buf, \{ it == c })
    }

    do {
        println(:F-READ-UNTIL-WHILE)
        val buf   = buf-init(iter("1 2 ;; 9\n3 ;;"))
        buf-read-until-c(buf, ';') thus {
            assert(it === "1 2 ")
        }
        buf-read-until-f(buf, \{ it=='\n'}) thus {
            assert(it === "; 9")
        }
        buf-read-while-f(buf, \{ it /= ';' }) thus {
            assert(it === "3 ")
        }
        buf-read-while-c(buf, ';') thus {
            assert(it === ";;")
        }
    }
}
